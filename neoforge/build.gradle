plugins {
    id 'com.github.johnrengelman.shadow'
    id 'dev.architectury.loom'
    id 'architectury-plugin'
}

loom {
    enableTransitiveAccessWideners.set(true)

    accessWidenerPath.set(project(":common").loom.accessWidenerPath)

    neoForge {
        //convertAccessWideners.set(true)
        //mixinConfig "gain_friendship_from_melodies.mixins.json"
    }
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

repositories {
    maven {
        name = 'NeoForged'
        url = 'https://neoforged.forgecdn.net/releases'
    }
}

dependencies {
    neoForge "net.neoforged:neoforge:${project.properties["neoForge_version"]}"
    modApi("dev.architectury:architectury-neoforge:${project.properties["architectury_version"]}")
    modApi("me.shedaniel.cloth:cloth-config-neoforge:${project.properties["cloth_config_lib_version"]}")

    runtimeOnly("maven.modrinth:ordsPcFz:${project.properties["kff_version"]}") //kotlinforforge
    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionNeoForge')
    modApi("maven.modrinth:immersive-melodies:${project.properties["immersive_melodies_version"]}-neoforge")
    modImplementation("com.cobblemon:neoforge:${project.properties["cobblemon_version"]}")
}

processResources {
    inputs.property 'version', project.version

    filesMatching('META-INF/mods.toml') {
        expand version: project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    input.set shadowJar.archiveFile
}
